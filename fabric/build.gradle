import groovy.json.JsonBuilder

plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
    modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }

    // https://modrinth.com/mod/modmenu/version/7.2.2
    modCompileOnly("maven.modrinth:modmenu:lEkperf6")

    modImplementation "io.wispforest:owo-lib:${project.owo_version}"
    include "io.wispforest:owo-sentinel:${project.owo_version}"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand([
            "mod_name": rootProject.mod_name,
            "mod_version": rootProject.mod_version,
            "mod_description": rootProject.mod_description,
            "mod_home_url": rootProject.mod_home_url,
            "mod_source_url": rootProject.mod_source_url,
            "copyright_licence": rootProject.copyright_licence,

            "minecraft_version": rootProject.minecraft_version,
            "fabric_loader_version": rootProject.fabric_loader_version,
        ])
        filter {
            (it as String).replace(
                "\"%FABRIC_AUTHORS_ARRAY%\"",
                new JsonBuilder((rootProject.mod_authors as String).tokenize(",")).toString()
            )
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "fabric"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

import net.fabricmc.loom.task.RemapJarTask

tasks.register("remapCivMc", RemapJarTask) {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "civmc-fabric"
    from sourceSets.main.output
    from "../civmc"
}

tasks.register("cleanJar", Delete) {
    delete fileTree("../dist") {
        include "*-fabric.jar"
    }
}

tasks.register("copyJar", Copy) {
    dependsOn cleanJar
    from remapJar, remapCivMc
    into "../dist"
}

build.dependsOn copyJar
