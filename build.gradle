// Update Gradle Wrapper using: ./gradlew wrapper --distribution-type bin --gradle-version <version>
// See Gradle's releases here: https://gradle.org/releases/

plugins {
    alias(libs.plugins.fabricLoom)
    //alias(libs.plugins.shadow)
}

dependencies {
    minecraft(libs.minecraft)
    mappings(loom.layered() {
        officialMojangMappings()
    })

    modImplementation(libs.fabricLoader)
    modImplementation(libs.fabricApi)

    modImplementation(libs.owoLib)
    include(libs.owoSentinel)

    modImplementation(libs.modMenu)

    implementation(libs.sqlite)
    // TODO: You should probably depend on https://modrinth.com/plugin/sqlite-jdbc instead
    include(libs.sqlite)

    compileOnly(libs.zstd)
    include(variantOf(libs.zstd) { classifier("linux_amd64") })
    include(variantOf(libs.zstd) { classifier("linux_aarch64") })
    include(variantOf(libs.zstd) { classifier("darwin_x86_64") })
    include(variantOf(libs.zstd) { classifier("darwin_aarch64") })
    include(variantOf(libs.zstd) { classifier("win_amd64") })
    include(variantOf(libs.zstd) { classifier("win_aarch64") })
}

base {
    archivesName = project.archives_base_name
}
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven { url 'https://maven.wispforest.io/releases/' }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

java {
    withSourcesJar()
}

processResources {
    filesMatching("assets/civmodern/lang/en_us.json") {
        expand([
                "mod_name": project.mod_name,
        ])
    }
    filesMatching("fabric.mod.json") {
        expand([
                "mod_name"             : project.mod_name,
                "mod_version"          : project.mod_version,
                "mod_description"      : project.mod_description,
                "mod_home_url"         : project.mod_home_url,
                "mod_source_url"       : project.mod_source_url,
                "copyright_licence"    : project.copyright_licence,

                "minecraft_version"    : libs.versions.minecraft.get(),
                "fabric_loader_version": libs.versions.fabricLoader.get(),
        ])
        filter {
            (it as String).replace(
                    "\"%FABRIC_AUTHORS_ARRAY%\"",
                    new JsonBuilder((project.mod_authors as String).tokenize(",")).toString()
            )
        }
    }
}

import groovy.json.JsonBuilder

tasks.register("cleanJar", Delete) {
    delete fileTree("dist") {
        include "*-fabric.jar"
    }
}

tasks.register("copyJar", Copy) {
    dependsOn cleanJar
    from remapJar
    into "dist"
}

build.dependsOn copyJar
